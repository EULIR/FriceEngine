group 'org.frice'
version '1.6.4'

buildscript {
	ext.kotlin_version = '1.2.0'
	repositories { mavenCentral() }
	dependencies { classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version" }
}

apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	mavenCentral()
	flatDir { dirs 'lib' }
}

sourceSets {
	main.kotlin.srcDirs += 'src'
	main.java.srcDirs += 'src'
	main.resources.srcDirs += 'res'
	test.kotlin.srcDirs += 'test'
	test.java.srcDirs += 'test'
}

configurations { artifact }

dependencies {
	compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
	compile group: 'org.jetbrains', name: 'annotations', version: '15.0'
	artifact name: 'jl1.0.1'
	artifact name: 'mp3spi1.9.5'
	artifact name: 'tritonus_share'

	configurations.compile.extendsFrom(configurations.artifact)

	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

task pureJar(type: Jar) {
	baseName = project.name + '-pure'
	from { configurations.artifact.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

task fatJar(type: Jar) {
	baseName = project.name + '-all'
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	with pureJar
}

